apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'


android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 33
        setProperty("archivesBaseName", "smart-table-view")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        aarMetadata {
            minCompileSdk = 17
        }
    }

    buildTypes {
        release {
            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    namespace 'ir.smartdevelopers.smarttable'
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}
group = 'ir.smartdevelopers'
version = '1.5.1'


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    // For control over item selection of both touch and mouse driven selection
    implementation "androidx.recyclerview:recyclerview-selection:1.2.0-alpha01"

}



publishing {

    publications {
        // Creates a Maven publication called "release".
        release(MavenPublication) {
            // Applies the component for the release build variant.
            afterEvaluate {
                from components.release
            }
            // You can then customize attributes of the publication as shown below.
            groupId = 'ir.smartdevelopers'
            artifactId = 'smart-table-view'
            pom {
                name = 'smart-table-view'
                description = 'Create table in android easily'
                url = 'https://github.com/smartdevelopers-ir/smart-table-view'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'smartdevelopers'
                        name = 'mostafa babaei'
                        email = 'info@smartdevelopers.ir'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/smartdevelopers-ir/smart-table-view.git'
                    developerConnection = 'scm:git:ssh://github.com/smartdevelopers-ir/smart-table-view.git'
                    url = 'http://github.com/smartdevelopers-ir/smart-table-view'
                }
            }
        }

    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}


signing {
    sign configurations.archives
}

